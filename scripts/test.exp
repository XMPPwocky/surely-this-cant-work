#!/usr/bin/expect -f
# Run the rvOS kernel test suite via serial console.
# Usage: expect scripts/test.exp

# Full suite took ~26s as of 02ff7e9.  120s gives ~5x headroom.
set timeout 120
log_user 1

cd [file dirname [file dirname [file normalize [info script]]]]

spawn make run-test
source [file dirname [info script]]/expect-cleanup.tcl

# Wait for the serial shell prompt (may have timer ticks interleaved)
expect {
    "rvos>" {}
    timeout { puts "TIMEOUT waiting for shell prompt"; exit 1 }
}

# Give a moment for any remaining boot output
sleep 1

send "run /bin/ktest\r"

# Wait for test completion marker (may take a while with spawn tests)
expect {
    "=== ALL TESTS PASSED ===" {
        # Wait for shell prompt, then shut down
        expect {
            "rvos>" {}
            timeout {}
        }
        send "shutdown\r"
        expect eof
        exit 0
    }
    "=== SOME TESTS FAILED ===" {
        expect {
            "rvos>" {}
            timeout {}
        }
        send "shutdown\r"
        expect eof
        exit 1
    }
    timeout { puts "TIMEOUT waiting for tests"; exit 1 }
}
